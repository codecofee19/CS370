' File: bubble.vbs
'
' Sort a list of numbers using a bubble sort

Option Explicit

const MINVALUE = 0
const MAXVALUE = 1000

dim a(20)                   ' array to demonstrate bubble sort

' Initialize the random generation system of VB-script.
' After this call, Rnd will return a number between 0 and 1.
Randomize

call fillWithRandomNumbers(a, MINVALUE, MAXVALUE)
call printOneArray(a, "An unsorted array")

call bubbleSort(a)
call printOneArray(a, "A sorted array")


'----------------------------------------------------------------------------
' bubbleSort
' Sort the input array using a bubble sort
SUB bubbleSort(ByRef a())
   dim pass                               ' pass of the sort
   dim done                               ' whether sorted or not 
   dim i, temp

   pass = 0
   done = FALSE

   ' Each pass puts one element into its sorted position,
   ' smallest value bubbles to the top of the array.
   while not done 
      done = TRUE                         ' possibly sorted

      ' compare consecutive elements, swap if out of order
      for i = UBound(a) to pass+1 step -1
         if a(i) < a(i-1) then
            temp = a(i)                      ' swap a(i) and a(i-1)
            a(i) = a(i-1)
            a(i-1) = temp
            done = FALSE                     ' had to swap so still not sorted
         end if
      next
      pass = pass + 1
   wend

end SUB

'----------------------------------------------------------------------------
' printOneArray
' Takes in 2 input parameters, the array to display and a string which
' is a description of what is displayed to be used as the MsgBox title.
' It displays the contents of the array in message box titled description.
SUB printOneArray(ByRef myArray, ByRef description)
   dim output, i
   output = ""
   for i = 0 to UBound(myArray)
      output = output & "myArray(" & i & ") = " & myArray(i) & vbNewLine
   next

   MsgBox output, ,description
end SUB

'----------------------------------------------------------------------------
' RandomNumber
' Given two input parameters, a min value and max value for a range of numbers,
' it will generate and return random number between the parameter values.
FUNCTION RandomNumber(ByVal min, ByVal max)
   RandomNumber = CInt( ((max - min + 1) * Rnd) + min)
end FUNCTION

'----------------------------------------------------------------------------
' fillWithRandomNumbers
' Given an input array, fill it with random numbers generated by a
' random number generator.
SUB fillWithRandomNumbers(ByRef a, ByRef minValue, ByRef maxValue)
   dim index

   'continually generate random numbers to fill the array
   for index = 0 to UBound(a)
      a(index) = RandomNumber(minValue, maxValue)
   next
end SUB

